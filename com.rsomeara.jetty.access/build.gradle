buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "org.sonarqube.gradle:gradle-sonarqube-plugin:1.1"
  }
}


apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.sonarqube'
apply plugin: 'jacoco'

//NOTE: If using Sonar of an earlier version than 5.x, the findbugs plug-in MUST be updated to 3.x or later in order to support Java 8
sourceCompatibility = 1.8

group = 'com.rsomeara'
version = '0.1-SNAPSHOT'

sourceSets{
	example
}

eclipse {
	classpath {
		plusConfigurations += [ configurations.exampleCompile, configurations.exampleRuntime ]
		downloadSources=true
		containers = [ 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8' ]
	}
}

repositories {
	mavenCentral()
}

task listAllDependencies(type: DependencyReportTask) {}

jar {
    manifest {
        attributes 'Implementation-Title': 'Jetty Server Access Handling',
                   'Implementation-Version': version
    }
}

task sourcesJar(type:Jar){
	from sourceSets.main.allSource
	classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

dependencies {
	compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.3.5.v20151012'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'
    
    exampleCompile group: 'args4j', name: 'args4j', version: '2.0.28'
    exampleCompile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.3.5.v20151012'
    
    exampleRuntime group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.7'
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}